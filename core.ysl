# temporary variables for use inside functions:
# i, j, n


# Classic Forth registers
var w = 0
var x = 0
var t = 0
var ip = 0

# Empty Forth stacks
var d-stack = ""
var dp = -1
var r-stack = ""
var rp = -1
var h-stack = ""
var hp = -1

# Input buffer
var input-buffer = ""
var >in = 0
var input-buffer-size = 0

# File interface
file.read "kernel.fs"
var current-file c return
size current-file
var current-file-size f return
# YSL doesn't allow empty strings in string_array for now
string_array n "This is the bottom. There's nothing else. Turn back."
var file-stack c return
string_array l file-stack
var file-stack-pointer f return
var file-stack-pointer - 1

# Temporary buffers
var tmp-buffers-num = 8
var tmp-buffer-size = 1024
var tmp-buffers = 0
var total-tmp-buffers-size = $tmp-buffer-size
var total-tmp-buffers-size * $tmp-buffers-num
set_size tmp-buffers $total-tmp-buffers-size
var next-tmp-buffer = 0

# Memory mapping addresses
var file-buffer-offset = -1048576
var input-buffer-offset = -256
var input-buffer-offset + $file-buffer-offset
var >in-offset = -1
var >in-offset + $input-buffer-offset
var tco-size-offset = -1
var tco-size-offset + $>in-offset
var tmp-buffers-offset = $tco-size-offset
var tmp-buffers-offset - $total-tmp-buffers-size
var state-offset = -1
var state-offset + $tmp-buffers-offset

# Dictionary
string_array n  "." "bye" "," "[" "]" ":" ";" "code" "compile" "exit"
var dictionary c return
var dict-refs  = 0   1     4   5   6   8   9   10     12        7
var immediates = 1   1     1  -1   1   1  -1   1      1         1
string_array l dictionary
var dictionary-pointer f return
var dictionary-pointer - 1

# Other variables
var base = 10
var state = 0
var here = *code-end
var source = $file-buffer-offset
var source-size = $current-file-size
var source-id = -1
var tco-size = 0

# Memory locations
var YSL-action-from-YSL-helper-addr = 2
var lit-action = 11
var exit-action = 7
var comma-action = 4
var jump-action = 13
var tail-call-action = 14

# Memory
#         0    1     2 3                                    4  5  6  7     8      9  10    11   12       13
var mem = *dot *halt 3 *execute-YSL-action-from-YSL-cleanup *, *[ *] *exit *colon *; *code *lit *compile *jump *tail-call
size mem
var mp f return
var mp - 1

# Startup
var x = *quit
gosub *execute-YSL-action-from-YSL
var x = *drop-file
gosub *execute-YSL-action-from-YSL
# println "Setting sauce to input"
var source = $input-buffer-offset
var source-size = $input-buffer-size
var >in = $source-size
var source-id = 0
goto *quit


# Basic Forth actions
next:
#   print "ToS: "
#   print $t
#   print " size: "
#   size d-stack
#   var w f return
#   print $w
#   print " IP: "
#   print $ip
#   print " Rdepth: "
#   size r-stack
#   var x f return
#   print $x
#   print " RP: "
#   print $rp
#   print " R: "
#   var x f r-stack $rp
#   println $x

    var w f mem $ip
    var ip + 1
    var x f mem $w
    var w + 1
    goto $x
 

enter:
    var r-stack a $ip
    var rp + 1
    var ip = $w
#   print "Entering "
#   println $ip
    goto *next


exit:
    var ip f r-stack $rp
    var r-stack r $rp 1
    var rp - 1
    goto *next


tail-call:
    var w f mem $ip
    var ip f r-stack $rp
    var r-stack r $rp 1
    var rp - 1
    var x f mem $w
    var w + 1
    goto $x


dovar:
    var d-stack a $t
    var dp + 1
    var t = $w
    goto *next


dodoes:
    var r-stack a $ip
    var rp + 1
    var ip f mem $w
    var w + 1
    var d-stack a $t
    var dp + 1
    var t = $w
    goto *next


lit:
    var d-stack a $t
    var dp + 1
    var t f mem $ip
    var ip + 1
    goto *next


jump:
    var ip f mem $ip
    goto *next


# Memory access
_@:
#   print "Getting value at "
#   println $x
    gt $x $mp
    goto_if *.out-of-bounds
    lt $x 0
    goto_if *.mapped
#   println "It's unmapped"
    var x f mem $x
    return $x
    # Mapped @
    .mapped:
        lt $x $file-buffer-offset
        goto_if *.input-buffer
#       println "It's file buffer"
        var x - $file-buffer-offset
        var x f current-file $x
        return $x
    .input-buffer:
        lt $x $input-buffer-offset
        goto_if *.>in
#       println "It's input buffer"
        var x - $input-buffer-offset
        var x f input-buffer $x
        return $x
    .>in:
        lt $x $>in-offset
        goto_if *.tmp-buffers
#       println "It's >in variable"
        return $>in
    .tmp-buffers:
        lt $x $tmp-buffers-offset
        goto_if *.state
#       println "It's tmp buffers"
        var x - $tmp-buffers-offset
        var x f tmp-buffers $x
        return $x
    .state:
        lt $x $state-offset
        goto_if *.out-of-bounds
#       println "It's state variable"
        return $state
    # Default out of bounds @
    .out-of-bounds:
#       println "It's out of bounds"
        return 0


+!:
    var x f d-stack $dp
    var dp - 1
    var w f mem $t
    var x + $w
    var mem s $t $x
    var t f d-stack $dp
    var d-stack r $dp 2
    var dp - 1
    goto *next


# ( x -- )
,:
    var mem a $t
    var mp + 1
    var t f d-stack $dp
    var d-stack r $dp 1
    var dp - 1
    var tco-size = 1
    goto *next


# I/O
dot:
    print $t
    print " "
    var t f d-stack $dp
    var d-stack r $dp 1
    var dp - 1
    goto *next


# -- input-buffer >in
_refill:
#   print "Input pointer at "
#   print $>in
#   print " for the buffer of size "
#   println $input-buffer-size
#   println "Refilling buffer..."
    cmp $source-id 0
    goto_if *.actually-refill
    return 0
    .actually-refill:
    input
    var input-buffer c return
    var >in = 0
    size input-buffer
    var input-buffer-size f return
    var source-size = $input-buffer-size
#   print "Refilled with "
#   print $input-buffer-size
#   println " chars"
    return 1


refill:
    var d-stack a $t
    var dp + 1
    gosub *_refill
    var t f return
    goto *next


# Parsing

# addr -- xt (1 | -1) | addr 0
find:
    var word = ""
    var word-addr = $t
    var x = $t
    gosub *_@
    var word-length f return
    lt $word-length 1
    goto *.no-copy-word
    .copy-word-loop:
        var t + 1
        var x = $t
        gosub *_@
        var x f return
        var word a $x
        var word-length - 1
        gt $word-length 0
        goto *.copy-word-loop
    .no-copy-word:
    gosub *_find
    var t f return
    cmp $t 0
    goto_if *.fail
        var d-stack a $x
        var dp + 1
        var t = 1
        goto *next
    .fail:
        var d-stack a $word-addr
        var dp + 1
        var t = 0
        goto *next


_find:
    var x = $dictionary-pointer
    .loop:
#       print "x = "
#       println $x
        string_array g dictionary $x
        var n c return
        gosub *cmp-string
        goto_if *.success
        var x - 1
        gt $x -1
        goto_if *.loop
        return 0
    .success:
        var n f immediates $x
        var x f dict-refs $x
        return $n


# Compilation
[:
    var state = 0
    goto *next

]:
    var state = -1
    goto *next


define:
    gosub *parse-word
#   print "Defining new word: "
#   println !word
    string_array a dictionary !word
    var immediates a 1
    var x = $mp
    var x + 1
    var dict-refs a $x
    var dictionary-pointer + 1
    return


colon:
    gosub *define
    var mem a *enter
    var mp + 1
    var state = -1
    var dictionary-pointer - 1
    var tco-size = 0
    goto *next


;:
    cmp $tco-size 0
    goto_if *.just-exit
        var w = $tco-size
        var x f mem $mp
        var mem a $x
        var w - 1
    .loop:
        cmp $w 0
        goto_if *.endloop
        var mp - 1
        var x f mem $mp
        var mp + 1
        var mem s $mp $x
        var mp - 1
        var w - 1
        goto *.loop
    .endloop:
        var mem s $mp $tail-call-action
        var mp + $tco-size
        var tco-size = 0
        goto *.end
    .just-exit:
        var mem a $exit-action
        var mp + 1
    .end:
        var dictionary-pointer + 1
        var state = 0
        goto *next


code:
#   print "dp before: "
#   println $dictionary-pointer
    gosub *define
#   print "dp after: "
#   println $dictionary-pointer
    var mem a $here
    var mp + 1
#   print "\$here is at "
#   println $here
    var x = *compile
    gosub *execute-YSL-action-from-YSL
    var tco-size = 0
    goto *next


compile:
    var addr = $source
    var addr + $>in
    .loop:
        # if input is over refill
        lt $>in $source-size
        goto_if *.no-refill
            cmp $source-id 0
            goto_if *.actually-refill
            goto *next
            .actually-refill:
            gosub *_refill
            var addr = $source
            var addr + $>in
            goto *.loop
        .no-refill:
        # check if starts with colon
        var x = $addr
        gosub *_@
        var x f return
        cmp $x &;
        goto_if *.end
        # parse until endl or end of input
        # store parsed code as string
        var line = ""
        .parse-loop:
            var line a $x
            var >in + 1
            var addr + 1
            cmp $>in $source-size
            goto_if *.parse-end
            var x = $addr
            gosub *_@
            var x f return
            cmp $x 10
            goto_if *.parse-newline-end
            goto *.parse-loop
        .parse-newline-end:
        var >in + 1
        var addr + 1
        .parse-end:
        # compile parsed code into program
#       println !line
        ysl.write_line $here !line
        var here + 1
        goto *.loop
    .end:
    var >in + 1
    goto *next


# YSL-Forth interface
execute-YSL-subroutine-from-forth:
    var x = $t
    var t f d-stack $dp
    var d-stack r $dp 1
    var dp - 1
    gosub $x
    goto *next


execute-YSL-action-from-forth:
    var x = $t
    var t f d-stack $dp
    var d-stack r $dp 1
    var dp - 1
    goto $x


execute-YSL-action-from-YSL:
    var r-stack a $ip
    var rp + 1
    var ip = $YSL-action-from-YSL-helper-addr
    goto $x
execute-YSL-action-from-YSL-cleanup:
    var ip f r-stack $rp
    var r-stack r $rp 1
    var rp - 1
    return


execute-Forth-word-from-YSL:
    var w = $x
    var x f mem $w
    var w + 1
    gosub *execute-YSL-action-from-YSL
    return


# Miscellaneoutousoutoeoosoeuusus
halt:
    exit


# Source code interpreter & compiler
quit:
#       print "Compilation state: "
#       println $state
        lt $>in $source-size
        goto_if *.no-refill
    .refill:
        cmp $source-id 0
        goto_if *.actually-refill
#       println "Exiting interpretation of string"
        goto *next
    .no-refill:
        gosub *parse-word
        size word
        var x f return
#       print "Size of word: "
#       println $x
        cmp $x 0
        goto_if *quit
        gosub *_find
        var found f return
#       print "found = "
#       println $found
        cmp $found 0
        goto_if *.number
        cmp $state 0
        goto_if *.execute
        cmp $found -1
        goto_if *.execute
        gosub *.compile
        var tco-size = 1
        goto *quit
    .compile:
        var mem a $x
        var mp + 1
        return
    .number:
        gosub *parse-number
        cmp $state 0
        goto_if *.push-number
        var tmp = $x
        var x = $lit-action
        gosub *.compile
        var x = $tmp
        gosub *.compile
        var tco-size = 0
        goto *quit
    .push-number:
        var d-stack a $t
        var dp + 1
        var t = $x
        goto *quit
    .execute:
#   print "Executing word "
#   println !word
#       print "Executing word at "
#       println $x
        gosub *execute-Forth-word-from-YSL
        goto *quit
    .actually-refill:
        cmp $state -1
        goto_if *.no-ok
        println "ok."
        .no-ok:
        gosub *_refill
        goto_if *.successful-refill
#       println "Unsuccessfull refill!"
        error
        .successful-refill:
        goto *quit



# input-buffer input-buffer-size >in -- word:w
parse-word:
#   println "Parsing whitespace..."
    gosub *parse-whitespace
#   println "Parsing name..."
    gosub *parse-non-whitespace
#   print "Parsed \""
#   print !word
#   println "\""
    return


# >in --
parse-whitespace:
    var addr = $source
    var addr + $>in
.loop:
    lt $>in $source-size
    goto_if *.no-end
    goto *.end
    .no-end:
    var x = $addr
    gosub *_@
    var x f return
    gt $x 32
    goto_if *.end
    var >in + 1
    var addr + 1
    goto *.loop
.end:
    return


# >in -- word
parse-non-whitespace:
    var word = ""
    var addr = $source
    var addr + $>in
.loop:
    cmp $>in $source-size
    goto_if *.end
#   println "Calling @..."
    var x = $addr
    gosub *_@
    var x f return
    var >in + 1
    var addr + 1
    lt $x 33
    goto_if *.end
    var word a $x
    goto *.loop
.end:
    return


# word:word word:n -- same?
cmp-string:
    size word
    var i f return
    size n
    var j f return
    cmp $i $j
    goto_if *.same-len
    return 0
.same-len:
    gt $i 0
    goto_if *.not-empty
    return 1
.not-empty:
    var i - 1
    var j f word $i
    var k f n $i
    cmp $j $k
    goto_if *.same-char
    return 0
.same-char:
    gt $i 0
    goto_if *.not-empty
    return 1
    

# word:word -- num:t
parse-number:
#   print "Parsing number "
#   println !word
    var x = 0
    var i = 1
    var tmp-base = $base
    var base = 10
    cmp $word &#
    goto_if *.sign
#   println "Not decimal"
    var base = 16
    cmp $word &$
    goto_if *.sign
#   println "Not hex"
    var base = 2
    cmp $word &%
    goto_if *.sign
#   println "Not binary"
    var base = $tmp-base
    cmp $word &'
    goto_if *.charnum
#   println "Not charnum"
    var i - 1
.sign:
    var max-digit = $base
    var max-digit - 1
#   println "Setting negative flag"
    var negative = 1
    var j f word $i
    var i + 1
    cmp $j &-
    goto_if *.negative
    var i - 1
    var negative = 0
.negative:
    size word
    var n f return
    var n - 1
    gt $i $n
    goto_if *.end
    var n + 1
    .parsing-loop:
        var j f word $i
        lt $j &a
        goto_if *.not-small-letters
        var j - 32
        .not-small-letters:
        lt $j &A
        goto_if *.not-big-letters
        var j - 7
        .not-big-letters:
        lt $j &0
        goto_if *.not-digits
        var j - &0
        .not-digits:
        lt $j 0
        goto_if *.error
        gt $j $max-digit
        goto_if *.error
#       print "Digit value: "
#       println $j
        var x * $base
        var x + $j
        var i + 1
        lt $i $n
        goto_if *.parsing-loop
    .end:
    var base = $tmp-base
    lt $negative 1
    goto_if *.nonnegative
        var x * -1
    .nonnegative:
    return
.charnum:
#   println "Yes charnum"
    var x f word 1
    return
.error:
    print "Digit of value "
    print $j
    print " at position "
    print $i
    print " in number "
    println !word
    error

code-end:
